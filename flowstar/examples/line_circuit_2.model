hybrid reachability
{
 state var v1,v2,t

 setting
 {
#  adaptive steps { min 0.005 , max 0.05 }
  fixed steps 0.01
  time 2.5
  remainder estimation 1e-3
  identity precondition
  gnuplot octagon t,v1
  adaptive orders { min 3 , max 6 }
  cutoff 1e-12
  precision 53
  output line_circuit_2
  max jumps 50
  print on
 }

 modes
 {
  l1
  {
   nonpoly ode
   {
    v1' = -2*v1 + v2 + 2 - exp(5*v1) - exp(5*(v1 - v2)) + 2
    v2' = -v2 + v1 - 1 + exp(5*(v1 - v2))
    t' = 1
   }
   inv
   {
    t <= 1
   }
  }

  l2
  {
   nonpoly ode
   {
    v1' = -2*v1 + v2 + 2 - exp(5*v1) - exp(5*(v1 - v2)) + (3 - t)
    v2' = -v2 + v1 - 1 + exp(5*(v1 - v2))
    t' = 1
   }
   inv
   {
    t >= 1
    t <= 2
   }
  }

  l3
  {
   nonpoly ode
   {
    v1' = -2*v1 + v2 + 2 - exp(5*v1) - exp(5*(v1 - v2)) + 1
    v2' = -v2 + v1 - 1 + exp(5*(v1 - v2))
    t' = 1
   }
   inv
   {
    t >= 2
   }
  }
 }

 jumps
 {
  l1 -> l2
  guard { t = 1 }
  reset { }
  interval aggregation

  l2 -> l3
  guard { t = 2 }
  reset { }
  interval aggregation
 }

 init
 {
  l1
  {
   v1 in [0,0.02]
   v2 in [0,0.02]
   t in [0,0]
  }
 }
}


hybrid reachability
{
 state var x, y, z

 setting
 {
  fixed steps 0.01
  time 7.5
  remainder estimation { x:[-1e-5,1e-5] , y:[-1e-5,1e-5], z:[-1e-5,1e-5] }
  QR precondition
  gnuplot octagon x,z
  adaptive orders { min 5 , max 8 }
  cutoff 1e-12
  precision 53
  output nonholonomic
  max jumps 1
  print on
 }

 modes
 {
  l1
  {
   poly ode 1
   {
    x' = 1
    y' = 1
    z' = x - y
   }

   inv
   {
    x^2 + y^2 - z <= 0
   }
  }

  l2
  {
   nonpoly ode
   {
    x' = -x + (2*y*z) / (x^2 + y^2)
    y' = -y - (2*x*z) / (x^2 + y^2)
    z' = -2*z
   }

   inv
   {

   }
  }
 }

 jumps
 {
  l1 -> l2

  guard
  {
   x^2 + y^2 - z = 0
  }

  reset
  {
  }

  parallelotope aggregation { }
 }

 init
 {
  l1
  {
   x in [0,0]
   y in [0,0]
   z in [14.9,15.1]
  }
 }
}
